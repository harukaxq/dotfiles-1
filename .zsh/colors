#
# Colors
# Modified from
# https://github.com/wincent/wincent/blob/7a8dcd92/roles/dotfiles/files/.zsh/colors
#

BASE16_DIR=~/.zsh/base16-shell/scripts
BASE16_CONFIG=~/.vim/.base16
BASE16_CONFIG_PREVIOUS=~/.vim/.base16.previous

# Takes a hex color in the form of "RRGGBB" and outputs its luma (0-255, where
# 0 is black and 255 is white)
luma() {
  local COLOR_HEX
  COLOR_HEX=$1

  if [ -z "$COLOR_HEX" ]
  then
    echo "Missing argument hex color (RRGGBB)"
    return 1
  fi

  # Extract hex channels from background color (RRGGBB)
  local COLOR_HEX_RED
  local COLOR_HEX_GREEN
  local COLOR_HEX_BLUE
  COLOR_HEX_RED=$(echo "$COLOR_HEX" | cut -c 1-2)
  COLOR_HEX_GREEN=$(echo "$COLOR_HEX" | cut -c 3-4)
  COLOR_HEX_BLUE=$(echo "$COLOR_HEX" | cut -c 5-6)

  # Convert hex colors to decimal
  local COLOR_DEC_RED
  local COLOR_DEC_GREEN
  local COLOR_DEC_BLUE
  COLOR_DEC_RED=$((16#$COLOR_HEX_RED))
  COLOR_DEC_GREEN=$((16#$COLOR_HEX_GREEN))
  COLOR_DEC_BLUE=$((16#$COLOR_HEX_BLUE))

  # Calculate perceived brightness of background per ITU-R BT.709
  # https://en.wikipedia.org/wiki/Rec._709#Luma_coefficients
  # http://stackoverflow.com/a/12043228/18986
  local COLOR_LUMA_RED
  local COLOR_LUMA_GREEN
  local COLOR_LUMA_BLUE
  COLOR_LUMA_RED=$((0.2126 * $COLOR_DEC_RED))
  COLOR_LUMA_GREEN=$((0.7152 * $COLOR_DEC_GREEN))
  COLOR_LUMA_BLUE=$((0.0722 * $COLOR_DEC_BLUE))

  local COLOR_LUMA
  COLOR_LUMA=$(($COLOR_LUMA_RED + $COLOR_LUMA_GREEN + $COLOR_LUMA_BLUE))

  echo "$COLOR_LUMA"
}

color() {
  SCHEME="$1"

  if [ $# -eq 0 -a -s "$BASE16_CONFIG" ]; then
    cat ~/.vim/.base16
    return
  fi

  case "$SCHEME" in
  help)
    echo 'color [tomorrow-night|ocean|grayscale-light|...]'
    echo
    echo 'Available schemes:'
    color ls
    ;;
  ls)
    find "$BASE16_DIR" -name 'base16-*.sh' | \
      sed -E 's|.+/base16-||' | \
      sed -E 's/\.sh//' | \
      column
      ;;
  -)
    local PREVIOUS_SCHEME
    PREVIOUS_SCHEME=$(head -n1 "$BASE16_CONFIG_PREVIOUS")
    color "$PREVIOUS_SCHEME"
    ;;
  *)
    # Check to see if the requested scheme is the same as the current scheme,
    # and if it is, bail out. This avoids overwriting the previous scheme file
    # with a duplicate, allowing `color -` to be more useful in more situations.
    if [[ -s "$BASE16_CONFIG" ]]; then
      local CURRENT_SCHEME
      CURRENT_SCHEME=$(head -n1 "$BASE16_CONFIG")
      if [ "$SCHEME" = "$CURRENT_SCHEME" ]; then
        return
      fi
    fi

    FILE="$BASE16_DIR/base16-$SCHEME.sh"
    if [[ -e "$FILE" ]]; then
      local BG
      local LUMA
      local LIGHT
      local BACKGROUND
      BG=$(grep color_background= "$FILE" | cut -d \" -f2 | sed -e 's#/##g')
      LUMA=$(luma "$BG")
      LIGHT=$((LUMA > 127.5))
      BACKGROUND=dark
      if [ "$LIGHT" -eq 1 ]; then
        BACKGROUND=light
      fi

      if [[ -s "$BASE16_CONFIG" ]]; then
        mv -f "$BASE16_CONFIG" "$BASE16_CONFIG_PREVIOUS"
      fi

      echo "$SCHEME" >! "$BASE16_CONFIG"
      echo "$BACKGROUND" >> "$BASE16_CONFIG"
      sh "$FILE"
    else
      echo "Scheme '$SCHEME' not found in $BASE16_DIR"
      echo "Looking for $FILE"
      return 1
    fi
    ;;
  esac
}

if [[ -s "$BASE16_CONFIG" ]]; then
  SCHEME=$(head -1 "$BASE16_CONFIG")
  BACKGROUND=$(sed -n -e '2 p' "$BASE16_CONFIG")
  if [ "$BACKGROUND" != 'dark' -a "$BACKGROUND" != 'light' ]; then
    echo "warning: unknown background type in $BASE16_CONFIG"
  fi
  color "$SCHEME"
else
  # default
  color phd
fi
